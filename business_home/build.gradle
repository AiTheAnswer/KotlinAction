if(isDebug.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28



    defaultConfig {
        if(isDebug.toBoolean()){
            applicationId "com.allen.business_home"
        }
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    /**
     * 但是当项目处于集成模式的时候，每一个业务组件的 AndroidManifest.xml 都要合并到“app壳工程”中
     * ，要是每一个业务组件都有自己的 Application 和 launch的Activity，那么合并的时候肯定会冲突。
     * 让业务组件在集成模式和组件模式下使用不同的AndroidManifest.xml
     */
    sourceSets{
        main{
            if(isDebug.toBoolean()){
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }else{
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}
kapt {
    generateStubs = true
    arguments {
        arg("moduleName", project.getName())
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':common')
    kapt 'com.alibaba:arouter-compiler:1.1.1'
}
